# =============================================================================
# MH3 Part 2: AWS Backup Plan (3 Resource Types)
# =============================================================================

resource "aws_backup_vault" "main" {
  name = "${var.project_name}-backup-vault"

  tags = {
    Name        = "${var.project_name}-backup-vault"
    Environment = var.environment
  }
}

resource "aws_iam_role" "backup" {
  name = "${var.project_name}-backup-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Effect = "Allow"
      Principal = {
        Service = "backup.amazonaws.com"
      }
      Action = "sts:AssumeRole"
    }]
  })

  tags = {
    Name = "${var.project_name}-backup-role"
  }
}

resource "aws_iam_role_policy_attachment" "backup" {
  role       = aws_iam_role.backup.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSBackupServiceRolePolicyForBackup"
}

resource "aws_iam_role_policy_attachment" "backup_restore" {
  role       = aws_iam_role.backup.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSBackupServiceRolePolicyForRestores"
}

resource "aws_backup_plan" "main" {
  name = "${var.project_name}-backup-plan"

  rule {
    rule_name         = "daily-backup"
    target_vault_name = aws_backup_vault.main.name
    schedule          = "cron(0 5 * * ? *)"

    lifecycle {
      delete_after = 7
    }
  }

  tags = {
    Name = "${var.project_name}-backup-plan"
  }
}

# Resource 1: EFS
resource "aws_backup_selection" "efs" {
  name         = "${var.project_name}-efs-backup"
  plan_id      = aws_backup_plan.main.id
  iam_role_arn = aws_iam_role.backup.arn

  resources = [
    aws_efs_file_system.app.arn
  ]
}

# Resource 2: DynamoDB
resource "aws_backup_selection" "dynamodb" {
  name         = "${var.project_name}-dynamodb-backup"
  plan_id      = aws_backup_plan.main.id
  iam_role_arn = aws_iam_role.backup.arn

  resources = [
    aws_dynamodb_table.conversations.arn
  ]
}

# Resource 3: EBS (App EC2 root volume)
resource "aws_backup_selection" "ebs" {
  name         = "${var.project_name}-ebs-backup"
  plan_id      = aws_backup_plan.main.id
  iam_role_arn = aws_iam_role.backup.arn

  resources = [
    "arn:aws:ec2:${var.aws_region}:${data.aws_caller_identity.current.account_id}:volume/*"
  ]

  condition {
    string_equals {
      key   = "aws:ResourceTag/Name"
      value = "${var.project_name}-app-instance"
    }
  }
}
